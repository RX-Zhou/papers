# load the color scheme
load '/home/schlipf/Dropbox/util/morgenstemning.gnu'

# define some variables
htr_to_meV = 27211.399
bohr_to_A = 0.52917721067
mass_vb = 0.14953
mass_cb = 0.13616
max_k = 0.0612572999995
lvb_1K = 0.170125668546
lvb_300K = 0.283689530544
lcb_1K = 0.164923785341
lcb_300K = 0.277960327358

# valence and conduction band
vb(x,l) = -0.5 * (x * bohr_to_A)**2 / (mass_vb * (1 + l)) * htr_to_meV
cb(x,l) =  0.5 * (x * bohr_to_A)**2 / (mass_cb * (1 + l)) * htr_to_meV

# number of points used in the plot
set samples 1000

# tics on top of heatmap
set tics front

# do not use a legend
unset key

# color box will be generated by last plot
set cbrange[2e-5:0.5]
set logscale cb
unset colorbox

# macros for plot
set macros
POS = "at graph 0.92,0.9"

# configuration of the x axis
set xrange[0:max_k]
set xtics 0, 0.02, 0.05 offset 0, 0.5
XT_OFF = "set format x ''"
XT_ON = "set format x '%g'"
XL_OFF = "unset xlabel"
XL_ON = "set xlabel '$k$\\thinspace(1/\\AA)' offset 0,1"

# configuration of the y axis
set ytics offset 0.8
YV_OFF = "set ytics ('' -60, '' -40, '' -20, '' 0); unset ylabel"
YV_ON = "set ytics ('$-60$' -60, '$-40$' -40, '$-20$' -20, '0' 0); set ylabel '$\\epsilon - \\epsilon\\pedex{v}$\\thinspace(meV)' offset 3.0"
YC_OFF = "set format y ''; unset ylabel"
YC_ON = "set format y '%.0f'; set ylabel '$\\epsilon - \\epsilon\\pedex{c}$\\thinspace(meV)' offset 2.0"

# valence and conduction band
CB = "set yrange[-15:75]; set ytics 0, 20, 60; set tmargin at screen 0.98; set bmargin at screen 0.55"
VB = "set yrange[-75:15]; set tmargin at screen 0.54; set bmargin at screen 0.11"

# split for different temperature
LEFT = "set lmargin at screen 0.07; set rmargin at screen 0.46"
RIGHT = "set lmargin at screen 0.48; set rmargin at screen 0.87"

# line colors
#EPI_OFF = "lc rgb '#00BFFF' lw 1.7"
EPI_OFF = "lc rgb '#1589ff' lw 2 dt 3"
EPI_ON = "lc 'black' lw 2"

set terminal epslatex color size 8.0cm, 6cm
set output 'tex/spectral.tex'

set multiplot layout 2,2

@XT_OFF; @XL_OFF; @YC_ON
@CB; @LEFT
set label 1 "{\\bfseries\\color{white}1\\thinspace{}K}" at 0.005, 70 front
plot 'data/new_model/spec_cb_1K.dat' with image, cb(x, 0) @EPI_OFF, cb(x, lcb_1K) @EPI_ON

@XT_OFF; @XL_OFF; @YC_OFF
@CB; @RIGHT
set label 1 "{\\bfseries\\color{white}300\\thinspace{}K}" at 0.008, 70 front
plot 'data/new_model/spec_cb_300K.dat' with image, cb(x, 0) @EPI_OFF, cb(x, lcb_300K) @EPI_ON

@XT_ON; @XL_OFF; @YV_ON
@VB; @LEFT
unset label 1
plot 'data/new_model/spec_vb_1K.dat' with image, vb(x, 0) @EPI_OFF, vb(x, lvb_1K) @EPI_ON

set colorbox user origin 0.90,0.11 size 0.02,0.87
set cbtics ("$10^{-4}$" 1e-4, "$10^{-3}$" 1e-3, "$10^{-2}$" 1e-2, "$10^{-1}$" 1e-1) offset -0.5
set label 2 at screen 0.925, 0.96 "$A_{k}(\\epsilon)$"
set label 3 at screen 0.925, 0.91 "($\\frac1{\\text{meV}}$)"
set label 4 at screen 0.47, 0.04 "$k$\\thinspace(1/\\AA)" center

@XT_ON; @XL_OFF; @YV_OFF
@VB; @RIGHT
plot 'data/new_model/spec_vb_300K.dat' with image, vb(x, 0) @EPI_OFF, vb(x, lvb_300K) @EPI_ON

unset multiplot
